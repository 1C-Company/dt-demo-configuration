

&НаКлиенте
Процедура Ответить(Команда)
	ПараметрыФормы = Новый Структура("ВходящееПисьмо", Объект.Ссылка);
	ОткрытьФорму("Справочник.ИсходящиеПисьма.ФормаОбъекта", ПараметрыФормы);
	Закрыть();
КонецПроцедуры


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Ключ.Пустая() Тогда
		Отбор = Новый Структура("Письмо", Параметры.Ключ);
		БылоПрочитано = РегистрыСведений.СостояниеВходящихПисем.Получить(Отбор).Прочитано;
		Если Не БылоПрочитано Тогда
			НЗ = РегистрыСведений.СостояниеВходящихПисем.СоздатьНаборЗаписей();
			НЗ.Отбор.Письмо.Установить(Параметры.Ключ);
			Запись = НЗ.Добавить();
			Запись.Письмо = Параметры.Ключ;
			Запись.Прочитано = Истина;
			НЗ.Записать();
		КонецЕсли;
		Если Объект.ВидСодержимого = Перечисления.ВидСодержимогоВходящегоПисьма.Текст Тогда
			Текст = Объект.Текст;
			Элементы.Группа.ТекущаяСтраница = Элементы.ГруппаТекст;
		Иначе
			HTML = Объект.Текст;
			Элементы.Группа.ТекущаяСтраница = Элементы.ГруппаHTML;
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не БылоПрочитано Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
		//Оповестить("ОбновитьСписокВходящихПисем");
	КонецЕсли;
КонецПроцедуры


 &НаСервере
Функция ПечатнаяФорма(ПараметрКоманды)
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ОтображатьСетку = Ложь;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	
	Сформирован = Ложь;
	
	Для каждого Ссылка Из ПараметрКоманды Цикл
		Объект = Ссылка.ПолучитьОбъект();
		Если Объект.ЭтоГруппа ИЛИ ПустаяСтрока(Объект.ШтрихКод) Тогда 
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Не задан штрихкод для '", "ru") + Строка(Объект);
			Сообщение.Поле  = "ШтрихКод";
			Сообщение.УстановитьДанные(Объект);
			Сообщение.Сообщить();
			Продолжить;
		КонецЕсли;	
		Объект.ПечатнаяФормаШтрихкода(ТабличныйДокумент);
		Сформирован = Истина;
	КонецЦикла;	
	
	Если Сформирован Тогда
		Возврат ТабличныйДокумент;
	Иначе 	
		Возврат Неопределено;
	КонецЕсли;	
	
КонецФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	ТабличныйДокумент = ПечатнаяФорма(ПараметрКоманды);
	
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать();
	КонецЕсли;	
	
КонецПроцедуры

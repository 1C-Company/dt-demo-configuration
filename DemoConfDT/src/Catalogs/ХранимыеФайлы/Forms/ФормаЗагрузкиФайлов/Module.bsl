
//////////////////////////////////////////////////////////////////////////////// 
// ОБРАБОТЧИКИ СОБЫТИЙ
//

&НаКлиенте
Процедура СписокФайловПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Отказ = Истина;
	ДобавитьФайлы();
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлы()
	
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ВыборФайла.МножественныйВыбор = Истина;
	ВыборФайла.Заголовок = НСтр("ru = 'Выбор файла'", "ru");
	ВыборФайла.Фильтр = НСтр("ru = 'Все файлы'", "ru") + " (*.*)|*.*";
	ВыборФайла.ПредварительныйПросмотр = Истина;
	
	ОпПослеВыбораФайлов = Новый ОписаниеОповещения(
		"ПослеВыбораФайлов", ЭтотОбъект);
	ВыборФайла.Показать(ОпПослеВыбораФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораФайлов(ВыбранныеФайлы, ДопПараметры) Экспорт
	Если НЕ ВыбранныеФайлы=Неопределено Тогда
		Для каждого Файл Из ВыбранныеФайлы Цикл
			СписокФайлов.Добавить(Файл);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКаталог()
	ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ОпПослеВыбораКаталога = Новый ОписаниеОповещения("ПослеВыбораКаталога", ЭтотОбъект);
	ВыборФайла.Показать(ОпПослеВыбораКаталога);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталога(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если ВыбранныеФайлы=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Каталог = ВыбранныеФайлы[0];
	ОпПослеПоискаФайлов = Новый ОписаниеОповещения("ПослеПоискаФайлов", ЭтотОбъект);
	НачатьПоискФайлов(ОпПослеПоискаФайлов, Каталог, "*.*", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПоискаФайлов(НайденныеФайлы, ДопПараметры) Экспорт
	Для каждого Файл Из НайденныеФайлы Цикл
		ОпПослеПроверкиЭтоФайл = Новый ОписаниеОповещения("ПослеПроверкиЭтоФайл", ЭтотОбъект, Файл.ПолноеИмя);
		Файл.НачатьПроверкуЭтоФайл(ОпПослеПроверкиЭтоФайл);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиЭтоФайл(ЭтоФайл, ПолноеИмяФайла) Экспорт
	Если ЭтоФайл Тогда
		СписокФайлов.Добавить(ПолноеИмяФайла);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Загрузить()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	Если СписокФайлов.Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Файлы не выбраны'", "ru");
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ПомещаемыеФайлы = Новый Массив;
	Для каждого Файл Из СписокФайлов Цикл
		Описание = Новый ОписаниеПередаваемогоФайла(Файл.Значение, "");
		ПомещаемыеФайлы.Добавить(Описание);
	КонецЦикла;
	
	ОпПослеПомещенияФайлов = Новый ОписаниеОповещения("ПослеПомещенияФайлов", ЭтотОбъект);
	НачатьПомещениеФайлов(ОпПослеПомещенияФайлов, ПомещаемыеФайлы, , Ложь);  

КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияФайлов(ПомещенныеФайлы, ДопПараметры) Экспорт
	Если НЕ ПомещенныеФайлы=Неопределено Тогда
		
		Для каждого Файл Из ПомещенныеФайлы Цикл
			СписокЗагруженныхФайлов.Добавить(Файл);
		КонецЦикла;
		Результат = Новый Структура(
			"СписокЗагруженныхФайлов, Владелец",
			СписокЗагруженныхФайлов, Владелец);
		Закрыть(Результат);
		
	Иначе
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Файлы не загружены'", "ru");
		Сообщение.Сообщить();
		
	КонецЕсли;
КонецПроцедуры

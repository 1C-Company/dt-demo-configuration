&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПользователиИнформационнойБазы.ТекущийПользователь().Имя = "" Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("РегистрацияИнформационнойБазыСистемыВзаимодействия", Метаданные) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	АдресСервера = "wss://1cdialog.com:443";
	ИмяИБ = Метаданные.КраткаяИнформация;
	
	Если СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована() Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОтменитьРегистрацию;
	Иначе
		Заголовок = НСтр("ru = 'Регистрация в системе взаимодействия'", "ru");
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаЗапросаКода;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКодРегистрации(Команда)
	
	ПараметрыРегистрации = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия();
	ПараметрыРегистрации.АдресСервера = АдресСервера;
	ПараметрыРегистрации.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
	ПараметрыРегистрации.ИмяИнформационнойБазы = ИмяИБ;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("КодРегистрацииОтправлен", ЭтотОбъект);
	СистемаВзаимодействия.НачатьРегистрациюИнформационнойБазы(ОписаниеОповещения, ПараметрыРегистрации);
	
КонецПроцедуры

&НаКлиенте
Процедура КодРегистрацииОтправлен(РегистрацияВыполнена, ТекстСообщения, ДопПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПредупреждениеКодОтправленПоказано", ЭтотОбъект);
	ПоказатьПредупреждение(ОписаниеОповещения, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупреждениеКодОтправленПоказано(ДопПараметры) Экспорт
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРегистрации;
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	ПараметрыРегистрации = Новый ПараметрыРегистрацииИнформационнойБазыСистемыВзаимодействия();
	ПараметрыРегистрации.АдресСервера = АдресСервера;
	ПараметрыРегистрации.АдресЭлектроннойПочты = АдресЭлектроннойПочты;
	ПараметрыРегистрации.ИмяИнформационнойБазы = ИмяИБ;
	ПараметрыРегистрации.КодАктивации = КодРегистрации;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("Зарегистрировано", ЭтотОбъект);
	СистемаВзаимодействия.НачатьРегистрациюИнформационнойБазы(ОписаниеОповещения, ПараметрыРегистрации);
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировано(РегистрацияВыполнена, ТекстСообщения, ДопПараметры) Экспорт
	
	Если НЕ РегистрацияВыполнена Тогда
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ВыполнитьИнициализацию();
	
	ПомощникКлиент.ПриНачалеРаботыСистемы();
	
	Закрыть();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьИнициализацию()
	
	Помощник.Инициализация();

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРегистрацию(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РегистрацияОтменена", ЭтотОбъект);
	СистемаВзаимодействия.НачатьОтменуРегистрацииИнформационнойБазы(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияОтменена(ДопПараметры) Экспорт
	
	Помощник.Отключение();
	Закрыть();

КонецПроцедуры


&НаКлиенте
Процедура EMailИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.ПолучитьКодРегистрации.Доступность =
		НЕ ПустаяСтрока(АдресСервера) И
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(ИмяИБ);

	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(АдресСервера) И
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(ИмяИБ) И
		НЕ ПустаяСтрока(КодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура АдресСервераИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.ПолучитьКодРегистрации.Доступность =
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(ИмяИБ);
		
	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(ИмяИБ) И
		НЕ ПустаяСтрока(КодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура ИмяИБИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.ПолучитьКодРегистрации.Доступность =
		НЕ ПустаяСтрока(АдресСервера) И
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(Текст);
	
	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(АдресСервера) И
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(Текст) И
		НЕ ПустаяСтрока(КодРегистрации);

КонецПроцедуры

&НаКлиенте
Процедура КодРегистрацииИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Элементы.Зарегистрировать.Доступность = 
		НЕ ПустаяСтрока(АдресСервера) И
		НЕ ПустаяСтрока(АдресЭлектроннойПочты) И
		НЕ ПустаяСтрока(ИмяИБ) И
		НЕ ПустаяСтрока(Текст);

КонецПроцедуры


&НаКлиенте
Процедура ПодтверждениеОтключенияПриИзменении(Элемент)
	
	Элементы.ОтменитьРегистрацию.Доступность = ПодтверждениеОтключения;

КонецПроцедуры
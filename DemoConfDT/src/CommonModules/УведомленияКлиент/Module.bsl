
// Процедура обработчик push-уведомлений и локальных уведомлений
Процедура ОбработкаУведомлений(Уведомление, Локальное, Показано, Параметры) Экспорт
	
#Если МобильныйКлиент Тогда
		
	Если не Показано Тогда
		// Иначе пользователь оповещен об уведомлении системными средствами.
		СредстваМультимедиа.ВоспроизвестиЗвуковоеОповещение();
	КонецЕсли;
	
	Если УведомленияСервер.ВоспроизводитьТекстУведомления() Тогда
		СредстваМультимедиа.ВоспроизвестиТекст(Уведомление.Текст);
	КонецЕсли;
	
#КонецЕсли
	
	Если Локальное = Истина Тогда
		Если СтрНачинаетсяС(Уведомление.Данные, "TN:") Тогда
			ПараметрыФормы = Новый Структура("Текст, Данные", Уведомление.Текст, Сред(Уведомление.Данные, 4));
	        ОткрытьФорму("ОбщаяФорма.Звонок",ПараметрыФормы);
        КонецЕсли;
	Иначе
		Если не Показано Тогда
			ПоказатьПредупреждение(, Уведомление.Текст);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

// Процедура получает идентификатор подписчика уведомлений
// - получает новый идентификатор
// - отправляет идентификатор источнику push-уведомлений
// - информирует пользователя о возникших ошибках
Процедура ОбновитьИдентификаторПодписчикаУведомлений() Экспорт
	
#Если МобильныйКлиент Тогда
	
		НомераПриложений = Новый Массив();
		НомерПриложения = УведомленияСервер.НомерПриложенияGoogleCloud();
		Если НомерПриложения <> "" 	Тогда
			НомераПриложений.Добавить(НомерПриложения);
		КонецЕсли;
		НомераПриложений.Добавить("797785657074"); // Идентификатор приложения на промежуточном сервисе 1С
		
		Попытка
			ИдентификаторПодписчикаУведомлений = ДоставляемыеУведомления.ПолучитьИдентификаторПодписчикаУведомлений(НомераПриложений);
			ТекстОшибки = "";
			УведомленияСервер.ПередатьИдентификаторПодписчикаУведомлений(ИдентификаторПодписчикаУведомлений, ТекстОшибки);
			Если  ТекстОшибки <> "" Тогда
				Сообщить(ТекстОшибки);
			КонецЕсли
		Исключение
	        Инфо = ИнформацияОбОшибке();
	        ПоказатьИнформациюОбОшибке(Инфо);
		КонецПопытки
	
#КонецЕсли

КонецПроцедуры
